use readable;
use readable::HeadExpression::*;

grammar;

pub Fun: readable::Function = {
    <fname:Name> <vars:Var*> "=" <body:Expr>
        => readable::Function {<>},
};

Var: readable::Var = {
    "(" <name: Name> ":" <typ: Expr> ")" => readable::Var {<>},
};

pub Expr: readable::Expression = {
    <head:HeadExprBox> <tail:TailExpr*> => readable::Expression {<>},
};

HeadExprBox: Box<readable::HeadExpression> = {
    HeadExpr => Box::new(<>),
};

HeadExpr: readable::HeadExpression = {
    Name => Name(<>.into()),

    "void_elim" <TailExpr> => VoidElim(<>),

    "tt" => Point,
    "unit_elim" <TailExpr> => UnitElim(<>),

    "true" => True,
    "false" => False,
    "bool_elim" <TailExpr> => BoolElim(<>),

    "pair" <TailExpr> <TailExpr> => Pair(<>),
    "sigma_elim" <TailExpr> <TailExpr> <TailExpr> => SigmaElim(<>),
};

TailExpr: readable::Expression = {
    "(" <Expr> ")",
    <head:HeadExprBox> => readable::Expression { <>, tail: Vec::new() },
};


Name: String = {
    r"[a-zA-Z_]+" => <>.into(),
};
